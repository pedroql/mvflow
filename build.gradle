buildscript {
    repositories {
        google()
        jcenter()
    }
    ext {
        kotlin_version = "1.3.72"
        detekt_version = "1.10.0"
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // AGP 4.x requires IntelliJ 2020.2
        classpath 'com.android.tools.build:gradle:3.6.4'
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "$kotlin_version"
    id "com.jfrog.bintray" version "1.8.5" apply false
    id "org.jetbrains.dokka" version "0.10.1"
    id "org.jmailen.kotlinter" version "2.4.1" apply false
    // when updating detekt, also update toolversion from detekt.build.gradle
    id "io.gitlab.arturbosch.detekt" version "$detekt_version" apply false
    id "org.jetbrains.kotlinx.binary-compatibility-validator" version "0.2.3"
}

allprojects {
    group = "net.pedroloureiro.mvflow"
    // when CI is making a release from a git tag, it will pass in the version
    // manually specify it with ./gradlew ... -DVERSION_OVERRIDE=123
    version = System.getProperty("VERSION_OVERRIDE", "0.0.4-SNAPSHOT")

    repositories {
        mavenCentral()
        jcenter()
        google()
    }
}

apply from: "$rootDir/dokka.build.gradle"
apply from: "$rootDir/apivalidation.build.gradle"
